&НаКлиенте
Процедура kinopoisk_unofficial_api_tokenПриИзменении(Элемент)
	УстановитьЗначениеКонстанты("kinopoisk_unofficial_api_token", kinopoisk_unofficial_api_token);
	УстановитьЗначениеКонстанты("ИспользуетсяЗаполнениеИзКинопоискаПоАПИ", НЕ kinopoisk_unofficial_api_token = "");
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	kinopoisk_unofficial_api_token = Константы.kinopoisk_unofficial_api_token.Получить();
	Стиль = Константы.СтильОформления.Получить();
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеКонстанты(ИмяКонстанты, ЗначениеКонстанты)
	Константы[ИмяКонстанты].Установить(ЗначениеКонстанты);
КонецПроцедуры

&НаКлиенте
Процедура СтильПриИзменении(Элемент)
	
	УстановитьЗначениеКонстанты("СтильОформления", Стиль);
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект);	
	ПоказатьВопрос(Оповещение, "Для применения нужно перезапустить программу. Перезапустить?", РежимДиалогаВопрос.ДаНет,10, КодВозвратаДиалога.Да,,КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗавершитьРаботуСистемы(Ложь, Истина);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьДанные(Команда)
	
	ПоказатьДлительнуюОперацию(Истина);	
	Оповещение = Новый ОписаниеОповещения("ВыборФайлаСохранение", ЭтотОбъект);
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога); 
	Диалог.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаСохранение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда		
		ПутьКФайлу = ВыбранныеФайлы[0];
		ИмяФайла = "Movie_backup_"+Формат(ТекущаяДата(),"ДФ=yyyy.MM.dd")+"_"+Формат(ТекущаяДата(),"ДФ=чч.мм.сс")+".json";
		Данные = СохранениеВосстановлениеДанныхСервер.Сохранить();
		ТекДок = Новый ЗаписьТекста(ПутьКФайлу + "\" + ИмяФайла);
		ТекДок.ЗаписатьСтроку(Данные);
		ТекДок.Закрыть();
		Сообщить("Сохранение данных завершено. Файл " + ИмяФайла + " сохранен.",СтатусСообщения.Внимание);
	КонецЕсли;	
	ПоказатьДлительнуюОперацию(Ложь);  
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДлительнуюОперацию(Показать)
	
	Элементы.kinopoisk_unofficial_api_token.Видимость = НЕ Показать;
	Элементы.ВосстановитьДанные.Видимость = НЕ Показать;
	Элементы.СохранитьДанные.Видимость = НЕ Показать; 
	Элементы.Стиль.Видимость = НЕ Показать;
	Элементы.ДлительнаяОперация.Видимость = Показать;
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьДанные(Команда)
	
	ПоказатьДлительнуюОперацию(Истина);	
	
	Оповещение = Новый ОписаниеОповещения("ВосстановлениеДанныхПродолжение", ЭтотОбъект);
	ТекстВопроса = "Все имеющиеся данные будут удалены и заполнены данными из файла. Продолжить?";
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 20, КодВозвратаДиалога.Нет,, КодВозвратаДиалога.Нет);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановлениеДанныхПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Оповещение = Новый ОписаниеОповещения("ВыборФайлаЧтение", ЭтотОбъект); 
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		Диалог.МножественныйВыбор = Ложь; 
		Диалог.Показать(Оповещение);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЧтение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда		
		ПутьКФайлу = ВыбранныеФайлы[0];
		ЧТ = Новый ЧтениеТекста(ПутьКФайлу);
		Текст = ЧТ.Прочитать();
		СохранениеВосстановлениеДанныхСервер.Восстановить(Текст);
		//Сообщить("Восстановление данных завершено.",СтатусСообщения.Внимание);
	КонецЕсли;	
	ПоказатьДлительнуюОперацию(Ложь);	
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект);	
	ПоказатьВопрос(Оповещение, "Восстановление данных завершено. Для применения нужно перезапустить программу. Перезапустить?", РежимДиалогаВопрос.ДаНет,10, КодВозвратаДиалога.Да,,КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура СправкаАПИ(Команда)
	Сообщить("Для использования автоматического заполнения данных по фильму зарегистрируйтесь на сайте https://kinopoiskapiunofficial.tech и введите полученный токен в поле.",СтатусСообщения.Внимание);
КонецПроцедуры
